
mod std::sort

use std::(rng, iter, opt)

pub proc ascending(f) {
    return |a, b| f(a) - f(b)
}

pub proc descending(f) {
    return |a, b| f(b) - f(a)
}  

pub proc quicksort(array, start, end, comp) {
    case end - start <= 1 -> return unit
    var current = {
        left = start,
        right = end - 1
    }
    var pivot = array[rng::rand_int_in(current.left, current.right)]
    exhaust(|| {
        current.left = current.left..=current.right
            |> iter::find(|l| comp(array[l], pivot) >= 0)
            |> opt::expect("'l' is guaranteed to meet the pivot")
        current.right = current.right..=current.left
            |> iter::find(|r| comp(array[r], pivot) <= 0)
            |> opt::expect("'r' is guaranteed to meet the pivot")
        case array[current.left] == pivot && array[current.right] == pivot
            -> return #end unit
        var moved = array[current.left]
        array[current.left] = array[current.right]
        array[current.right] = moved
        return #next unit
    })
    quicksort(array, start, current.left, comp)
    quicksort(array, current.right + 1, end, comp)
}